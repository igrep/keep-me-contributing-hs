apply plugin: 'com.android.application'

buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:2.3.3'
    }
}

android {
    compileSdkVersion 26
    buildToolsVersion '25.0.0'
    defaultConfig {
        applicationId "info.igreque.keepmecontributinghs"
        minSdkVersion 24
        targetSdkVersion 26
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        disable 'InvalidPackage'
    }

    compileOptions {
        sourceCompatibility = 1.7
        targetCompatibility = 1.7
    }

    // http://anton0825.hatenablog.com/entry/2015/01/16/154147
     packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/ASL2.0'
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.0', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile 'org.kohsuke:github-api:1.90'
    compile 'com.android.support:appcompat-v7:26.0.0-alpha1'
    compile 'com.android.support:support-v4:26.0.0-alpha1'
    testCompile 'junit:junit:4.12'
}

String etaJarPath = '../dist/build/keep-me-contributing-hs/keep-me-contributing-hs.jar'

task etlasBuild(type: Exec) {
    workingDir '..'
    // https://functor.tokyo/blog/2017-07-28-ghc-warnings-you-should-enable
    commandLine(
        'etlas',
        'build',
        '--eta-option=-Wall',
        '--eta-option=-fwarn-incomplete-uni-patterns',
        '--eta-option=-fwarn-incomplete-record-updates',
    )
    // Causes an error:
    //   Failed to capture snapshot of input files for task 'etlasNewBuild' property '$1' during up-to-date check
    //   > Could not normalize path for file 'C:\Users\igrep\Dropbox\prg\prj\KeepMeContributingHs\app\src\**\*.hs'.
    // inputs.files "src/**/*.hs"
    def hsFiles = []
    new File(projectDir, 'src').eachFileRecurse { file ->
        if (file.name.endsWith('.hs')) {
            hsFiles << file
        }
    }
    inputs.files(hsFiles)
    outputs.file etaJarPath
}

task installEtaJar(type: Copy, dependsOn: 'etlasBuild') {
    from etaJarPath
    into 'libs/'
}

project.afterEvaluate {
    prepareDebugDependencies.dependsOn 'installEtaJar'
}

task printClasspaths {
    doLast {
        configurations.compile.each { println it }
    }
}
