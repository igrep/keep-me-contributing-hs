apply plugin: 'com.android.application'

buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:2.3.3'
    }
}

android {
    compileSdkVersion 26
    buildToolsVersion '25.0.0'
    defaultConfig {
        applicationId "info.igreque.keepmecontributinghs"
        minSdkVersion 24
        targetSdkVersion 26
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        disable 'InvalidPackage'
    }

    compileOptions {
        sourceCompatibility = 1.7
        targetCompatibility = 1.7
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.0', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile 'com.android.support:appcompat-v7:26.+'
    compile 'com.android.support:support-v4:26.+'
    testCompile 'junit:junit:4.12'
}

String etaJarPath = '../dist-newstyle/build/x86_64-windows/eta-0.0.9.2/eta-first-0.1.0.0/c/eta-first/build/eta-first/eta-first.jar'

task etlasNewBuild(type: Exec) {
    workingDir '..'
    commandLine 'etlas', 'new-build'
    // Causes an error:
    //   Failed to capture snapshot of input files for task 'etlasNewBuild' property '$1' during up-to-date check
    //   > Could not normalize path for file 'C:\Users\igrep\Dropbox\prg\prj\KeepMeContributingHs\app\src\**\*.hs'.
    // inputs.files "src/**/*.hs"
    outputs.file etaJarPath
}

task installEtaJar(type: Copy, dependsOn: 'etlasNewBuild') {
    from etaJarPath
    into 'libs/'
}

project.afterEvaluate {
    prepareDebugDependencies.dependsOn 'installEtaJar'
}
